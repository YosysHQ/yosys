pattern xilinx_bram_pack
state <SigSpec> sigDOA sigDOPA sigDOB

state <SigBit> clock
state <Cell*> ffDOA ffDOAcemux ffDOB ffDOBcemux
state <bool> ffDOAcepol ffDOBcepol
state <Cell*> ffDOPA ffDOPAcemux
state <bool> ffDOPAcepol

// Variables used for subpatterns
state <SigSpec> argQ argD
state <bool> ffcepol ffrstpol
state <int> ffoffset
udata <SigSpec> dffD dffQ
udata <SigBit> dffclock
udata <Cell*> dff dffcemux dffrstmux
udata <bool> dffcepol dffrstpol

// (1) Starting from a RAMB18E1 cell
match bram
	select bram->type.in(\RAMB18E1, \RAMB36E1)
endmatch

// Select the correct consumers of DOADO port
code sigDOA
	if (param(bram, \DOA_REG, 0).as_int() == 0) {
                SigSpec DOA = port(bram, \DOADO);
		for (int i = 0; i < GetSize(DOA); i++) 
		{
			// Ignore unused output bits
			if (nusers(DOA[i]) < 2)
				continue;
			// Only collect output bits with a single sink
			else if (nusers(DOA[i]) == 2)
                                sigDOA.append(DOA[i]);
			// Abandon this whole output if any bit
			//   has more than a single sink
                        else if (nusers(DOA[i]) > 2) {
                                sigDOA = SigSpec();
                                break;
                        }
		}

	}
endcode

// Traverse the consumers of DOADO
code argD ffDOA ffDOAcemux ffDOAcepol sigDOA clock
	if (!sigDOA.empty()) {
		argD = sigDOA;
		clock = port(bram, \CLKARDCLK);
		subpattern(out_dffe);
		if (dff) {
			ffDOA = dff;
			if (dffcemux) {
				ffDOAcemux = dffcemux;
				ffDOAcepol = dffcepol;
			}
			sigDOA = dffQ;
		}
	}
endcode

// Select the correct consumers of DOAPADOP port
code sigDOPA
	if (param(bram, \DOA_REG, 0).as_int() == 0) {
                SigSpec DOPA = port(bram, \DOPADOP);
		for (int i = 0; i < GetSize(DOPA); i++) 
		{
			// Ignore unused output bits
			if (nusers(DOPA[i]) < 2)
				continue;
			// Only collect output bits with a single sink
			else if (nusers(DOPA[i]) == 2)
                                sigDOPA.append(DOPA[i]);
			// Abandon this whole output if any bit
			//   has more than a single sink
                        else if (nusers(DOPA[i]) > 2) {
                                sigDOPA = SigSpec();
                                break;
                        }
		}

	}
endcode

// Traverse the consumers of DOADO
code argD ffDOPA ffDOPAcemux ffDOPAcepol sigDOPA clock
	if (!sigDOPA.empty()) {
		argD = sigDOPA;
		clock = port(bram, \CLKARDCLK);
		subpattern(out_dffe);
		if (dff) {
			ffDOPA = dff;
			if (dffcemux) {
				ffDOPAcemux = dffcemux;
				ffDOPAcepol = dffcepol;
			}
			sigDOPA = dffQ;
		}
	}
endcode

//================== PORT B =============================//
code sigDOB
	if (param(bram, \DOB_REG, 0).as_int() == 0) {
                SigSpec DOB = port(bram, \DOBDO);
		for (int i = 0; i < GetSize(DOB); i++)
		{
			// Ignore unused output bits
			if (nusers(DOB[i]) < 2)
				continue;
			// Only collect output bits with a single sink
			else if (nusers(DOB[i]) == 2)
                                sigDOB.append(DOB[i]);
			// Abandon this whole output if any bit
			//   has more than a single sink
                        else if (nusers(DOB[i]) > 2) {
                                sigDOB = SigSpec();
                                break;
                        }
		}
	}

endcode

code argD ffDOB ffDOBcemux ffDOBcepol sigDOB clock
	if (!sigDOB.empty()) {
		argD = sigDOB;
		clock = port(bram, \CLKARDCLK);
		subpattern(out_dffe);
		if (dff) {
			ffDOB = dff;
			if (dffcemux) {
				ffDOBcemux = dffcemux;
				ffDOBcepol = dffcepol;
			}
			sigDOB = dffQ;
		}
	}
endcode
 
code
	log_dump(sigDOA, ffDOA, ffDOAcemux);
	accept;
endcode

subpattern out_dffe
arg argD argQ clock
 
code
	dff = nullptr;
	for (auto c : argD.chunks())
		// Abandon matches when 'D' has the keep attribute set
		if (c.wire->get_bool_attribute(\keep))
			reject;
endcode
 
// (1) Starting from an optional $mux cell that implements clock enable
//     semantics --- one where the given 'D' argument (partially or fully)
//     drives one of its two inputs
match ffcemux
	select ffcemux->type.in($mux)
	// ffcemux output must have two users: ffcemux and ff.D
	select nusers(port(ffcemux, \Y)) == 2
 
	choice <IdString> AB {\A, \B}
	// keep-last-value net must have at least three users: ffcemux, ff, downstream sink(s)
	select nusers(port(ffcemux, AB)) >= 3
 
	slice offset GetSize(port(ffcemux, \Y))
	define <IdString> BA (AB == \A ? \B : \A)
	index <SigBit> port(ffcemux, BA)[offset] === argD[0]
 
	// Check that the rest of argD is present
	filter GetSize(port(ffcemux, BA)) >= offset + GetSize(argD)
	filter port(ffcemux, BA).extract(offset, GetSize(argD)) == argD
 
	set ffoffset offset
	define <bool> pol (AB == \A)
	set ffcepol pol
 
	semioptional
endmatch
 
code argD argQ
	dffcemux = ffcemux;
	if (ffcemux) {
		SigSpec BA = port(ffcemux, ffcepol ? \B : \A);
		SigSpec Y = port(ffcemux, \Y);
		argQ = argD;
		argD.replace(BA, Y);
		argQ.replace(BA, port(ffcemux, ffcepol ? \A : \B));
 
		dffcemux = ffcemux;
		dffcepol = ffcepol;
	}
	log_dump(ffcemux, argD, argQ);
endcode
 
// (3) Match for a $dff cell (whose 'D' input is the 'D' argument, or the
//     output of the previous clock enable or reset $mux cells)
match ff
	select ff->type.in($dff)
	
	slice offset GetSize(port(ff, \D))
	index <SigBit> port(ff, \D)[offset] === argD[0] 
 
	// Check that offset is consistent
	filter (!ffcemux) || ffoffset == offset
	// Check that the rest of argD is present
	filter GetSize(port(ff, \D)) >= offset + GetSize(argD)
	filter port(ff, \D).extract(offset, GetSize(argD)) == argD
	// Check that FF.Q is connected to CE-mux
	filter !ffcemux || port(ff, \Q).extract(offset, GetSize(argQ)) == argQ
 
	filter clock == SigBit() || port(ff, \CLK) == clock
 
	set ffoffset offset
endmatch
 
code argQ
	SigSpec D = port(ff, \D);
	SigSpec Q = port(ff, \Q);
	if (!ffcemux) {
		argQ = argD;
		argQ.replace(D, Q);
	}
 
	dff = ff;
	dffQ = argQ;
	dffclock = port(ff, \CLK);
	log_dump(ff, dff, dffQ, dffclock);
endcode
